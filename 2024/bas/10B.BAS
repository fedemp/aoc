10 OPEN "I", #1, "INPUT10.TXT"
20 LIMIT=56: DIM MAP%(LIMIT+1, LIMIT+1): X=-1
30 IF EOF(1) GOTO 100
40 LINE INPUT #1, L$
50 X = X + 1
60 FOR I=0 TO LIMIT
70 MAP%(X, I) = VAL(MID$(L$, I+1, 1))
80 NEXT I
90 GOTO 30
100 CLOSE #1: DIM PATHS(4,2)
110 PATHS(0,0) = 0: PATHS(0,1) = -1: PATHS(1,0) = 0: PATHS(1,1) = 1
120 PATHS(2,0) = -1: PATHS(2,1) = 0: PATHS(3,0) = 1: PATHS(3,1) = 0
130 OPEN "R", #2, "PATH10.TXT", 16: FIELD #2, 16 AS V$
140 TOTAL# = 0 
150 RECA = -1: RECB = 0 'ALTERNATE BETWEEN EACH RECORD
160 FOR X=0 TO LIMIT 'FOR EACH LINE
170 FOR Y=0 TO LIMIT 'FOR EACH CHARACTER
180 IF MAP%(X,Y) <> 0 GOTO 440
190 FOR P=0 TO 3
200 PX = X + PATHS(P, 0): PY = Y + PATHS(P, 1)
210 IF PX < 0 OR PY < 0 OR PX > LIMIT OR PY > LIMIT GOTO 250
220 IF MAP%(PX,PY) <> 1 GOTO 250
230 RECA = RECA + 2
240 LSET V$=RIGHT$(STR$(PX),2)+RIGHT$(STR$(PY),2): PUT #2, RECA
250 NEXT P
260 CURR = 1
270 WHILE RECA > 0
280 GET #2, RECA
290 VX = VAL(LEFT$(V$, 2)): VY = VAL(MID$(V$, 3, 2))
300 FOR P=0 TO 3
310 PX = VX + PATHS(P, 0): PY = VY + PATHS(P, 1)
320 IF PX < 0 OR PY < 0 OR PX > LIMIT OR PY > LIMIT GOTO 390
330 IF MAP%(PX,PY) <> CURR+1 GOTO 390
340 REM NEXT IS VALID, BUT IS IT THE SUMMIT?
350 IF CURR=8 AND MAP%(PX,PY)=9  THEN TOTAL#=TOTAL#+1: GOTO 390
360 REM ELSE SAVE NEXT STEP
370 RECB = RECB + 2
380 LSET V$=RIGHT$(STR$(PX),2)+RIGHT$(STR$(PY),2): PUT #2, RECB
390 NEXT P
400 RECA = RECA - 2
410 WEND
420 SWAP RECA, RECB: CURR = CURR + 1
430 IF CURR < 9 AND RECA > 0 GOTO 270
440 NEXT Y: NEXT X
450 PRINT "TOTAL:" TOTAL#: END
