10 OPEN "I", #1, "INPUT11.TXT"
20 S=32: NULL$ = "": FOR I=1 TO S: NULL$ = NULL$ + CHR$(0): NEXT I
30 OPEN "R", #2, "OUTPUT11.TXT", S: FIELD #2, S AS V$ 
40 RECA# = -1: RECB# = 0
50 LINE INPUT #1, LN$
60 WHILE INSTR(LN$, " ") > 0
70 RECA# = RECA# + 2
80 LSET V$ = MKD$(VAL(MID$(LN$, 1, INSTR(LN$, " ")-1))): PUT #2, RECA#
90 LN$ = MID$(LN$, INSTR(LN$, " ") + 1)
100 WEND
110 RECA# = RECA# + 2
120 LSET V$ = MKD$(VAL(LEFT$(LN$, LEN(LN$) - 1))): PUT #2,RECA# 'SAVE LAST VALUE
130 FOR I=1 TO 25
140 IF RECB# > 1 THEN SWAP RECA#, RECB#
150 INIT = ( -( RECA# - INT(RECA#/2)*2 ) ) 'INITIAL RECORD NUMBER
160 IF INIT THEN RECB# = 0 ELSE RECB# = -1
170 FOR REC=INIT+2 TO RECA# STEP 2
180 GET #2, REC: N# = CVD(V$): RECB# = RECB# + 2
190 IF N# = 0 THEN LSET V$ = MKD$(1): PUT #2, RECB#: GOTO 290
200 REM SECOND RULE
210 DGTS = INT(LOG(N#)/LOG(10)) + 1
220 IF DGTS MOD 2 = 1 GOTO 270
230 LSET V$ = MKD$(INT(N#/10^(DGTS/2))): PUT #2, RECB#: 'FIRST HALF
240 RECB# = RECB# + 2 'SECOND HALF
250 LSET V$=MKD$(N# - INT(N#/10^(DGTS/2)) * 10^(DGTS/2)): PUT #2, RECB#
260 GOTO 290
270 REM DEFAULT RULE
280 LSET V$ = MKD$(CVD(V$) * 2024): PUT #2, RECB#
290 NEXT REC
300 NEXT I
310 PRINT "TOTAL:" RECA#
320 END
