10 OPEN "I", #1, "INPUT12.TXT"
20 LIMIT =139: DIM BOARD%(LIMIT + 1,LIMIT + 1)
30 FOR X=0 TO LIMIT: LINE INPUT #1, LN$
40 FOR Y=0 TO LIMIT: BOARD%(X,Y) = ASC(MID$(LN$, Y+1, 1))
50 NEXT Y: NEXT X: CLOSE #1
60 DIM MOVES(4,2)
70 MOVES(0,0) = 1: MOVES(0,1) = 0: MOVES(1,0) = 0: MOVES(1,1) = 1
80 MOVES(2,0) = -1: MOVES(2,1) = 0: MOVES(3,0) = 0: MOVES(3,1) = -1
90 OPEN "R", #1, "TREE12.TXT", 32: FIELD #1, 16 AS TX$, 16 AS TY$
100 TOTAL# = 0: FOR X=0 TO LIMIT: FOR Y=0 TO LIMIT
110 IF BOARD%(X,Y) < 0 GOTO 260 'Visited cell
130 REC = 1: PERI = 0: AREA = 0
140 LSET TX$=MKI$(X): LSET TY$=MKI$(Y): PUT #1, REC
150 WHILE REC > 0
160 GET #1, REC: REC = REC - 1: CX = CVI(TX$): CY = CVI(TY$)
170 IF BOARD%(CX,CY) < 0 GOTO 235 ELSE AREA = AREA + 1
180 FOR I=0 TO 3: NX = CX + MOVES(I,0): NY = CY + MOVES(I,1)
190 IF NX > LIMIT OR NY > LIMIT OR NX < 0 OR NY < 0 THEN PERI=PERI+1: GOTO 220
200 IF BOARD%(CX,CY) <> ABS(BOARD%(NX,NY)) THEN PERI=PERI+1: GOTO 220
205 IF BOARD%(NX,NY) < 0 GOTO 220
210 LSET TX$=MKI$(NX): LSET TY$=MKI$(NY): REC=REC+1: PUT #1, REC
220 NEXT I
230 BOARD%(CX,CY) = -BOARD%(CX,CY) 'Mark as visited
235 WEND
250 TOTAL# = TOTAL# + AREA * PERI
260 NEXT Y: NEXT X
270 PRINT "TOTAL:" TOTAL#: END
