10 OPEN "I",#1,"INPUT12.TXT":OPEN "O",#2,"T12.TXT":CLOSE #2:KILL "T12.TXT"
20 LIMIT=140: DIM B%(LIMIT + 2,LIMIT + 2)
30 FOR X=1 TO LIMIT: LINE INPUT #1, LN$
40 FOR Y=1 TO LIMIT: B%(X,Y) = ASC(MID$(LN$, Y, 1))
50 NEXT Y: NEXT X: CLOSE #1: DIM M(4,3,2)
60 M(0,0,0)=-1: M(0,0,1)=0: M(0,1,0)=0: M(0,1,1)=1: M(0,2,0)=-1: M(0,2,1)=1
70 M(1,0,0)=0: M(1,0,1)=1: M(1,1,0)=1: M(1,1,1)=0: M(1,2,0)=1: M(1,2,1)=1
80 M(2,0,0)=1: M(2,0,1)=0: M(2,1,0)=0: M(2,1,1)=-1: M(2,2,0)=1: M(2,2,1)=-1
90 M(3,0,0)=0: M(3,0,1)=-1: M(3,1,0)=-1: M(3,1,1)=0: M(3,2,0)=-1: M(3,2,1)=-1
100 OPEN "R", #1, "T12.TXT", 16: FIELD #1, 8 AS TX$, 8 AS TY$
110 TOTAL# = 0: FOR X=1 TO LIMIT: FOR Y=1 TO LIMIT
120 IF B%(X,Y) < 0 GOTO 290 'Visited cell
130 REC = 1: PERI = 0: AREA = 0
140 LSET TX$=MKI$(X): LSET TY$=MKI$(Y): PUT #1, REC
150 WHILE REC > 0
160 GET #1, REC: REC = REC - 1: CX = CVI(TX$): CY = CVI(TY$): CURR=B%(CX,CY)
170 IF CURR < 0 GOTO 280 ELSE B%(CX,CY) = -CURR: AREA = AREA + 1
180 FOR S=0 TO 3
190 NX = CX + M(S,0,0): NY = CY + M(S,0,1): N=B%(NX,NY)
200 IF ABS(N) <> CURR GOTO 230
210 IF B%(NX,NY) < 0 GOTO 270
220 LSET TX$=MKI$(NX): LSET TY$=MKI$(NY): REC = REC + 1: PUT #1, REC: GOTO 270
230 OTHER1 = ABS(B%(CX + M(S,1,0), CY + M(S,1,1)))
240 OTHER2 = ABS(B%(CX + M(S,2,0), CY + M(S,2,1)))
250 IF OTHER1 <> CURR THEN PERI=PERI + 1: GOTO 270
260 IF OTHER2 = CURR THEN PERI=PERI + 1
270 NEXT S
280 WEND: TOTAL# = TOTAL# + AREA * PERI
290 NEXT Y: NEXT X
300 PRINT "TOTAL:" TOTAL#: END
