10 INITT$=TIME$
20 OPEN "i", #1, "input.txt"
30 TOTAL#= 0 : LIMIT = 12: DIM NUMS(LIMIT): DIM SORTED(LIMIT)
40 IF EOF(1) THEN GOTO 270
50 LINE INPUT #1, LN$
60 PREVSP = 1: I = 0: ERASE NUMS : DIM NUMS(LIMIT)
70 COLON = INSTR(LN$, ":")
80 RES# = VAL(LEFT$(LN$, COLON)) : NUMS$ = RIGHT$(LN$, LEN(LN$) - COLON - 1)
90 SP = INSTR(PREVSP+1, NUMS$, " ")
100 IF SP<>0 THEN NUMS(I)=VAL(MID$(NUMS$,PREVSP,SP-PREVSP))
110 IF SP<>0 THEN PREVSP=SP: I=I+1: GOTO 90
120 IF SP=0 THEN NUMS(I) = VAL(RIGHT$(NUMS$,LEN(NUMS$)-PREVSP)) ELSE GOTO 90
130 PRINT LN$
140 BASE=2: FOR T=0 TO (2^I)-1: GOSUB 280: TOT#=RES#: VALID=1
150 FOR X=0 TO I-1: GOSUB 340
160 IF TOT# < NUMS(0) OR VALID=0 THEN GOTO 190
170 NEXT X
180 IF TOT# = NUMS(0) THEN TOTAL# = TOTAL# + RES# : GOTO 40
190 NEXT T
200 BASE=3: FOR T=0 TO (3^I)-1: GOSUB 280: TOT#=RES#: VALID=1
210 FOR X=0 TO I-1: GOSUB 340
220 IF T - 3 * INT(T/3) <> 2 GOTO 240
230 IF TOT# < NUMS(0) OR VALID=0 GOTO 260
240 NEXT X
250 IF TOT# = NUMS(0) THEN TOTAL# = TOTAL# + RES# : GOTO 40
260 NEXT T: GOTO 40
270 PRINT INITT$ " : " TIME$: PRINT "TOTAL" TOTAL#: END
280 REM SUB: CONVERT TO BASE
290 BASED#=0: IF T=0 THEN RETURN
300 N#=T: IX#=0: WHILE N# > 0
310 REMAIN = N# - (BASE * INT(N#/BASE))
320 BASED#=BASED# + REMAIN * 10^IX#: IX#=IX#+1
330 N#=INT(N#/BASE): WEND: RETURN
340 REM SUB: DO OPERATIONS
350 NUMB=NUMS(I-X):
360 OPER# = INT(BASED# / 10^X): OPER# = OPER#-INT(OPER#/10)*10
370 IF OPER#=0 THEN TOT# = TOT# - NUMB ELSE GOTO 390
380 IF TOT# < NUMS(0) THEN VALID=0: RETURN ELSE RETURN
390 IF OPER#=1 THEN GOTO 400 ELSE GOTO 430
400 IF (TOT#-NUMB * INT(TOT#/NUMB)) = 0 GOTO 410 ELSE GOTO 420
410 TOT# = TOT#/NUMB: RETURN
420 VALID=0: RETURN
430 REM TRY CONCAT
440 TOT# = TOT# - NUMB: DGTS = INT(LOG(NUMB)/LOG(10))+1
450 IF NOT TOT# - INT(TOT#/10^DGTS)*(10^DGTS) = 0 THEN VALID=0: RETURN
460 TOT# = TOT# / 10^DGTS: RETURN
470 REM SUB: IS CONCATENATION POSSIBLE FROM THE START
480 DGTS = INT(LOG(NUMS(0)) / LOG(10))+1
490 RDGTS = INT(LOG(RES#) / LOG(10))+1
500 IF INT(RES# / 10^(RDGTS-DGTS)) <> NUMS(0) THEN VALID=0
510 RETURN
