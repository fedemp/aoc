10 OPEN "i", #1, "INPUT8.TXT"
20 LIMIT=11: DIM M(LIMIT + 1,LIMIT +1): TOTAL = 0: ROW=0
30 IF EOF(1) GOTO 90 ELSE LINE INPUT #1, LN$
50 FOR COL=0 TO LIMIT
60 M(ROW,COL) = ASC(MID$(LN$, COL+1, 1))
70 NEXT COL: ROW=ROW+1: GOTO 30
90 COD=0: FOR ROW=0 TO LIMIT: FOR COL=0 TO LIMIT
100 IF M(ROW,COL) <> 46 AND M(ROW,COL) <> 1046 THEN GOSUB 2000
110 NEXT COL: NEXT ROW
120 PRINT "TOTAL:" TOTAL: END
2000 REM SUB: FIND A PAIR
2005 IF M(ROW,COL) > 1000 THEN COD=M(ROW,COL) MOD 1000 ELSE COD=M(ROW,COL)
2010 FOR SOW=ROW+1 TO LIMIT: FOR DOL=0 TO LIMIT
2020 IF M(SOW,DOL) = COD THEN GOSUB 3000
2030 NEXT DOL: NEXT SOW
2040 RETURN
3000 REM SUBSUB: CREATE ANTINODES
3010 DY=SOW-ROW: NY=SOW+DY
3020 DX=DOL-COL: NX=DOL+DX
3030 IF NY<=LIMIT AND NX>=0 AND NX<=LIMIT THEN GOSUB 4000 ELSE GOTO 3050
3050 NY=ROW-DY: DX=-DX: NX=COL+DX
3060 IF NY>=0 AND NX>=0 AND NX<=LIMIT THEN GOSUB 4000
3070 RETURN
4000 REM ADD TO TOTAL IF POSSIBLE
4005 PRINT ROW+1;COL+1;SOW+1; DOL+1;NY+1;NX+1
4010 IF M(NY,NX) < 1000 THEN TOTAL=TOTAL+1: M(NY,NX) = M(NY,NX) MOD 1000 +1000
4020 RETURN
