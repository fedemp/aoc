10 OPEN "i", #1, "INPUT8.TXT"
15 OPEN "o", #2, "OUTPUT.TXT"
20 LIMIT=49: DIM M(LIMIT + 1,LIMIT +1): TOTAL=0: ROW=0
30 IF EOF(1) GOTO 90 ELSE LINE INPUT #1, LN$
50 FOR COL=0 TO LIMIT
60 M(ROW,COL) = ASC(MID$(LN$, COL+1, 1))
70 NEXT COL: ROW=ROW+1: GOTO 30
90 COD=0: FOR ROW=LASTROW TO LIMIT: FOR COL=0 TO LIMIT
95 IF M(ROW,COL) > 1000 THEN COD = M(ROW,COL) MOD 1000 ELSE COD=M(ROW,COL)
100 IF COD <> 46 THEN GOSUB 2000
110 NEXT COL: NEXT ROW
120 PRINT "TOTAL:" TOTAL: END
2000 REM SUB: FIND A PAIR
2010 PCOD=0: FOR SOW=ROW+1 TO LIMIT: FOR DOL=0 TO LIMIT
2015 IF M(SOW,DOL) > 1000 THEN PCOD=M(SOW,DOL) MOD 1000 ELSE PCOD=M(SOW,DOL)
2020 IF PCOD = COD THEN GOSUB 3000
2030 NEXT DOL: NEXT SOW
2040 RETURN
3000 REM SUBSUB: CREATE ANTINODES
3002 IF M(ROW,COL) < 1000 THEN M(ROW,COL) = COD + 1000: TOTAL=TOTAL+1
3003 IF M(SOW,DOL) < 1000 THEN M(SOW,DOL) = PCOD + 1000: TOTAL=TOTAL + 1
3010 DY=SOW-ROW: NY=SOW+DY
3020 DX=DOL-COL: NX=DOL+DX
3030 WHILE NX>=0 AND NY<=LIMIT AND NX<=LIMIT
3040 GOSUB 4000 : NX=NX+DX : NY=NY+DY: WEND
3050 NY=ROW-DY: DX=-DX: NX=COL+DX
3060 WHILE NX>=0 AND NX<=LIMIT AND NY>=0
3070 GOSUB 4000: NX=NX+DX: NY=NY-DY: WEND
3080 RETURN
4000 REM ADD TO TOTAL IF POSSIBLE
4011 IF M(NY,NX) < 1000 THEN PRINT #2, ROW COL SOW DOL NX NY
4020 IF M(NY,NX) < 1000 THEN TOTAL=TOTAL+1: M(NY,NX) = (M(NY,NX) MOD 1000)+1000
4030 RETURN
