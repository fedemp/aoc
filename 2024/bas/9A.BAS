10 OPEN "I", #1, "INPUT9.TXT"
20 KILL "FILES.TXT": OPEN "R", #2, "FILES.TXT", 32: FIELD#2, 32 AS V$
30 TY=-1: RECID=0: ID# = 0 'BLOCK TYPE. RECORD ID. FILE ID
40 C$ = INPUT$(1, #1): IF EOF(1) GOTO 100 
50 TY=( TY + 1 ) MOD 2: FOR I=1 TO VAL(C$)
60 IF TY = 0 THEN LSET V$=STR$(ID#) ELSE LSET V$="."
70 RECID = RECID + 1: PUT #2, RECID: 
80 NEXT I: IF TY=0 THEN ID#=ID# + 1
90 GOTO 40
100 CLOSE #1: LSP = 2 'LSP = LAST USED SPACE
110 FOR RX=RECID TO LSP STEP -1
120 GET #2, RX
130 IF LEFT$(V$,1) = "." GOTO 220
140 CURR$ = V$
150 FOR I=LSP TO RX
160 GET #2, I
170 IF LEFT$(V$,1) <> "." GOTO 210
180 LSET V$ = CURR$: PUT #2, I
190 LSET V$ = ".": PUT #2, RX
200 LSP = I: GOTO 220
210 NEXT I
220 NEXT RX
230 TOTAL# = 0: POSI# = 0
240 FOR I=1 TO RECID
250 GET #2, I
260 IF LEFT$(V$,1) = "." GOTO 280
270 TOTAL# = TOTAL# + VAL(V$) * POSI#: POSI# = POSI# + 1
280 NEXT I
290 PRINT "TOTAL:" TOTAL#: END
